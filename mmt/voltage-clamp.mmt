[[model]]
name: voltage_clamp_model
display_name: Lei et al., 2020 Voltage clamp model
mmt_authors: Chon Lok Lei, Michael Clerx
version: 20210824
desc: """
    Voltage clamp model with standard compensation [1].

    [1] Accounting for variability in ion current recordings using a
        mathematical model of artefacts in voltage-clamp experiments.
        Chon Lok Lei, Michael Clerx, Dominic G. Whittaker, David J. Gavaghan,
        Teun P. de Boer, Gary R. Mirams.
        Phil. Trans. R. Soc. A, 378: 20190348, 2020.
        https://doi.org/10.1098/rsta.2019.0348
"""
membrane.V            = -80
voltage_clamp.V_clamp = -80
voltage_clamp.V_p     = -80
voltage_clamp.V_est   = -80
voltage_clamp.I_out   =  0

#
# Simulator variables: To be replaced with model variables
#
[engine]
time = 0 [ms]
    in [ms]
    bind time
pace = 0
    bind pace

#
# Cell size: To be replaced with model variable
#
[cell]
Cm = 98.7109 [pF]
    in [pF]
    desc: Cell capacitance

#
# Membrane potential: To be replaced with model version, modified as shown
# below.
#
[membrane]
use voltage_clamp.V_p, voltage_clamp.R_series, voltage_clamp.V_offset_eff
use voltage_clamp.I_leak
use cell.Cm
I_ion = 0
    in [A/F]
dot(V) = (V_p + V_offset_eff - V) / (Cm * R_series) - (I_ion + I_leak)
    in [mV]

#
# Voltage-clamp model
#
[voltage_clamp]
use cell.Cm
use membrane.V
V_c = engine.pace * 1 [mV]
    desc: Command voltage
    in [mV]
Cm_est = 100
    in [pF]
    desc: Estimated cell capacitance
R_series = 10e-3 [GOhm]
    in [GOhm]
    desc: Series resistance (~MegaOhms)
R_series_est = 10e-3 [GOhm]
    in [GOhm]
    desc: Estimated series resistance
alpha = 0.7
    desc: Percentage compensation
C_prs = 4 [pF]
    in [pF]
    desc: 'Parasitic'/pipette capacitance at electrode
C_prs_est = 3 [pF]
    in [pF]
    desc: Estimated 'parasitic'/pipette capacitance at electrode

V_offset_eff = 5 [mV]
    in [mV]
    desc: Effective voltage offset

tau_sum = 40e-3 [ms]
    in [ms]
    desc: Summation delay (approximately 5--70 us)
dot(V_clamp) = (V_c + (I_out * Cm + Cm_est * dot(V_est)) * R_series_est * alpha - V_clamp) / tau_sum
    desc: Clamp voltage
    in [mV]

tau_clamp = 0.8e-3 [ms]
    in [ms]
    desc: Clamp delay
dot(V_p) = (V_clamp - V_p) / tau_clamp
    in [mV]
    desc: Pipette voltage

tau_out = 7.5e-3 [ms]
    in [ms]
    desc: Feedback delay
dot(I_out) = (I_in - I_out) / tau_out
    in [A/F]
    desc: Measured current

# Supercharging
# From https://doi.org/10.1007/978-1-4419-1229-9_4 Fig. 18
dot(V_est) = piecewise(
        Cm_est < 1e-6, 0,
        R_series_est < 1e-6, 0,
        (V_c - V_est) / ((1 - alpha) * Cm_est * R_series_est)
    )
    in [mV]
    desc: Supercharging voltage

# Leak current, assumed to be linear
g_leak = 2 [pA/mV]
    in [1/GOhm]
    desc: Leak conductance (thought as inverse of seal resistance)
E_leak = -80 [mV]
    in [mV]
    desc: Leak reversal potential
I_leak = g_leak * (V - E_leak) / Cm
    in [A/F]
    desc: Leak current

I_in = (
        (V_p - V + V_offset_eff) / R_series
        + C_prs * dot(V_p)
        - C_prs_est * dot(V_clamp)
        - Cm_est * if(R_series_est < 1e-6, dot(V_clamp), dot(V_est))
    ) / Cm
    in [A/F]

# Leak subtracted (post-processed) current
g_leak_est = 1.5 [pA/mV]
    in [nS]
    desc: Estimated leak conductance
I_post = I_out - g_leak_est * (V_c - E_leak) / Cm
    in [A/F]
    desc: Leak subtracted current
    label measured_current

